//===== rAthena Script =======================================
//= Battleground: PVP
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A simple battleground script:
//= Kill players from the other team.
//===== Additional Comments: =================================
//= 1.0 First version, edited. [Euphy]
//= 1.1 Use up to date battleground script commands [AnnieRuru]
//============================================================

-	script	5v5pvp#control	-1,{
OnInit:
	.minplayer2start = 1;      // minimum players to start (ex. if 3vs3, set to 3)
	.eventlasting    = 10*60;  // event duration before auto-reset (20 minutes * seconds)
	setarray .rewarditem[0],   // rewards for the winning team: <item>,<amount>,...
		501, 10;
	end;
OnStart:
	if ( getwaitingroomstate( 0, .rednpcname$ ) < .minplayer2start || getwaitingroomstate( 0, .bluenpcname$ ) < .minplayer2start )
		end;

	// create Battleground and teams
	.red = waitingroom2bg( "cen_pvp01-2", 79,120, strnpcinfo(0)+"::OnRedQuit", strnpcinfo(0)+"::OnRedDead", .rednpcname$ );
	.blue = waitingroom2bg( "cen_pvp01-2", 120,80, strnpcinfo(0)+"::OnBlueQuit", strnpcinfo(0)+"::OnBlueDead", .bluenpcname$ );
	delwaitingroom .rednpcname$;
	delwaitingroom .bluenpcname$;
	bg_warp .red, "cen_pvp01-2", 79,120;
	bg_warp .blue, "cen_pvp01-2", 120,80;
	.red_score = .blue_score = .minplayer2start;
	bg_updatescore "cen_pvp01-2", .red_score, .blue_score;

	// match duration
	sleep .eventlasting * 1000;

	// end match, destroy Battleground, reset NPCs
	if ( .red_score > .blue_score ) {
		mapannounce "cen_pvp01-2", "- Red Team is victorious! -", bc_map;
		callsub L_Reward, .red;
	}
	else if ( .blue_score > .red_score ) {
		mapannounce "cen_pvp01-2", "- Blue Team is victorious! -", bc_map;
		callsub L_Reward, .blue;
	}
	else
		mapannounce "cen_pvp01-2", "- The match has ended in a draw! -", bc_map;
	bg_warp .red, "ma_in01",18,15;
	bg_warp .blue, "ma_in01",18,13;
	bg_destroy .red;
	bg_destroy .blue;
	donpcevent .rednpcname$ +"::OnStart";
	donpcevent .bluenpcname$ +"::OnStart";
	end;

L_Reward:
	bg_get_data getarg(0), 1;
	for ( .@i = 0; .@i < $@arenamemberscount; ++.@i )
		getitem .rewarditem[0], .rewarditem[1], $@arenamembers[.@i];
	return;

// "OnDeath" event
OnRedDead:  callsub L_Dead, .red_score;
OnBlueDead: callsub L_Dead, .blue_score;
L_Dead:
	set getarg(0), getarg(0) -1;
	bg_updatescore "cen_pvp01-2", .red_score, .blue_score;
	bg_leave;
	if ( !getarg(0) )
		awake strnpcinfo(0);
	sleep2 1250;
	percentheal 100,100;
	end;

// "OnQuit" event
OnRedQuit:  callsub L_Quit, .red_score;
OnBlueQuit: callsub L_Quit, .blue_score;
L_Quit:
	set getarg(0), getarg(0) -1;
	bg_updatescore "cen_pvp01-2", .red_score, .blue_score;
	percentheal 100, 100;
	if ( !getarg(0) )
		awake strnpcinfo(0);
	end;
}

ma_in01,23,20,5	script	Red Team#5v5pvp	733,{
	end;
OnInit:
	sleep 1;
	set getvariableofnpc( .rednpcname$, "5v5pvp#control" ), strnpcinfo(0);
OnStart:
	waitingroom "Red Team", getvariableofnpc( .minplayer2start, "5v5pvp#control" ) +1, "5v5pvp#control::OnStart", getvariableofnpc( .minplayer2start, "5v5pvp#control" );
    setlook LOOK_CLOTHES_COLOR,121;
	end;
}

ma_in01,23,17,5	script	Blue Team#5v5pvp	734,{
	end;
OnInit:
	sleep 1;
	set getvariableofnpc( .bluenpcname$, "5v5pvp#control" ), strnpcinfo(0);
OnStart:
	waitingroom "Blue Team", getvariableofnpc( .minplayer2start, "5v5pvp#control" ) +1, "5v5pvp#control::OnStart", getvariableofnpc( .minplayer2start, "5v5pvp#control" );
    setlook LOOK_CLOTHES_COLOR,102;
	end;
}

cen_pvp01-2	mapflag	gvg	off
cen_pvp01-2	mapflag	battleground	2
cen_pvp01-2	mapflag	nosave	SavePoint
cen_pvp01-2	mapflag	nowarp
cen_pvp01-2	mapflag	nowarpto
cen_pvp01-2	mapflag	noteleport
cen_pvp01-2	mapflag	nomemo
cen_pvp01-2	mapflag	nopenalty
cen_pvp01-2	mapflag	nobranch
cen_pvp01-2	mapflag	noicewall
cen_pvp01-2	mapflag	hidemobhpbar
